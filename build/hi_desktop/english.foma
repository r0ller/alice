### english.foma ###

# Vowels
define V [ a | e | i | o | u ];
# Consonants
define C [ b | c | d | f | g | h | j | k | l | m | n | p | q | r | s | t | v | w | x | y | z ];

define PotentialWord [C|V]+;

# E insertion e added after -s, -z, -x, -ch, -sh before s (watch/watches)
define EInsertion [..] -> e || s | z | x | c h | s h _ "^" s ;

# Y replacement: -y changes to -ie before -s, -i before -ed (try/tries)
define YReplacement y -> i e || _ "^" s ;

define YthReplacement y -> i e || _ "^" t h ;

# Cleanup: remove morpheme boundaries
define Cleanup "^" -> 0;

read lexc engnum.lexc 
define	LexiconNum

define	GrammarNum LexiconNum .o.
		YthReplacement        .o.
		Cleanup;

read lexc engnoun.lexc 
substitute defined PotentialWord for "[Guess]"
define LexiconN

define GrammarN LexiconN         .o.
               EInsertion        .o.
               YReplacement      .o.
               Cleanup;

read lexc english.lexc
define Lexicon

define Grammar Lexicon           .o.
               Cleanup;

read lexc engpunct.lexc
define LexiconPunct

define GrammarPunct LexiconPunct      .o. 
                Cleanup;

regex GrammarN | GrammarNum | Grammar | GrammarPunct;
